# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_srcname=SPIRV-LLVM-Translator
pkgname=lib32-${_srcname,,}
pkgver=18.1.1
pkgrel=1
pkgdesc="Tool and a library for bi-directional translation between SPIR-V and LLVM IR"
arch=(x86_64)
url="https://github.com/KhronosGroup/SPIRV-LLVM-Translator"
license=(LicenseRef-custom)
depends=(lib32-llvm-libs lib32-spirv-tools ${pkgname#lib32-})
makedepends=(git cmake llvm lib32-llvm spirv-headers)
# Current point in the used LLVM branch
_commit=2d31bdaccf937c7b045d6a8ea705ebb52f2aaaa8
source=(git+${url}.git#commit=$_commit)
sha256sums=('SKIP')

#pkgver() {
#  cd ${_srcname}
#  printf "18.0.0.r%s+%s"  "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
#}

#prepare() {
#  cd ${_srcname}
#  patch -Np1 -i ../0001-Fix-standalone-builds-with-LLVM_LINK_LLVM_DYLIB-ON.patch
#}

build() {
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  cmake -B build -S ${_srcname} \
    -G 'Unix Makefiles' \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS:STRING=-m32 \
    -DCMAKE_C_FLAGS:STRING=-m32 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
    -DCMAKE_SKIP_RPATH=ON \
    -DLLVM_CONFIG="/usr/bin/llvm-config32" \
    -DLLVM_INCLUDE_TESTS=OFF \
    -DBASE_LLVM_VERSION="18.1.0" \
    -DLLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=/usr/include/spirv/ \
    -DLLVM_LIBDIR_SUFFIX=32 \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  rm -r "${pkgdir}"/usr/{bin,include}
  install -Dm644 ${_srcname}/LICENSE.TXT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
