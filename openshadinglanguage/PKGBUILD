# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openshadinglanguage
pkgver=1.12.14.0
pkgrel=2
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'imath' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses' 'clang')
makedepends=('boost' 'cmake' 'python' 'llvm' 'ninja')
optdepends=('python: the Python module')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/archive/refs/tags/v${pkgver}.tar.gz
        llvm17.patch::https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/commit/cdfec4f31d2658179f1bda1cf34491b95ad37a7e.patch)
sha512sums=('51641e58fb0bba042853e3976c104decc788f1b1d3ec2e33a3f5504b11b05950dc38766ffb066b0f76edc4e0a8829d6b7dd408d9288b74d69380395b1d991bd5')

prepare() {
  cd OpenShadingLanguage-$pkgver
  patch -Np1 -i ../llvm17.patch
}

build() {
  cd OpenShadingLanguage-$pkgver

  cmake \
    -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=17 \
    -DLLVM_DIRECTORY=/usr/lib/llvm \
    -DLLVM_STATIC=0 \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenShadingLanguage-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
