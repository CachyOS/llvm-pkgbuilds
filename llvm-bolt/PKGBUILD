# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=llvm-bolt
pkgver=16.0.0rc3
_pkgver=16.0.0-rc3
pkgrel=1
pkgdesc="Post Link Optimizer from LLVM"
arch=('x86_64')
url="https://llvm-bolt.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('llvm-libs')
makedepends=('llvm' 'cmake' 'ninja' 'python-sphinx')
options=('!lto') # LTO causes a couple of test failures
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$_pkgver
source=($_source_base/bolt-$pkgver.src.tar.xz{,.sig}
        $_source_base/llvm-$pkgver.src.tar.xz{,.sig}
        $_source_base/cmake-$pkgver.src.tar.xz{,.sig}
        $_source_base/third-party-$pkgver.src.tar.xz{,.sig})
sha256sums=('f5a7ebf1cc2e1c1e5a686284afab392ed3c837f8bbe7e0fed9812ba711f4c7e4'
            'SKIP'
            '631eae5682409d890651fbb8d81580f703be45fecf2d67c1f9f07662ea5ab98b'
            'SKIP'
            '892b720979983735618137d19faaa6ac28500c1d7cdf7c0c4489f1ab0531f154'
            'SKIP'
            'e56f362fded1d2f936620edacace4d07ee60b2912534b8821531b96da0d3489c'
            'SKIP')

prepare() {
  # https://bugs.llvm.org/show_bug.cgi?id=49228
  mv cmake{-$pkgver.src,}
  mv third-party{-$pkgver.src,}
  mv bolt{-$pkgver.src,}
  cd llvm-$pkgver.src
  mkdir build
}

build() {
  cd llvm-$pkgver.src/build

  local cmake_args=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_DOCDIR=share/doc
    -DCMAKE_SKIP_RPATH=ON
    -DLLVM_BUILD_DOCS=ON
    -DLLVM_ENABLE_SPHINX=ON
    -DLLVM_ENABLE_PROJECTS="bolt"
    -DLLVM_INCLUDE_TESTS=ON
    -DLLVM_LINK_LLVM_DYLIB=ON
    -DSPHINX_WARNINGS_AS_ERRORS=OFF
  )
  cmake .. "${cmake_args[@]}"
  ninja llvm-bolt
}

check() {
  cd llvm-$pkgver.src/build
  ninja check-bolt
}

package() {
  cd llvm-$pkgver.src/build

  DESTDIR="$pkgdir" ninja install-llvm-bolt install-perf2bolt install-merge-fdata \
        install-llvm-boltdiff install-bolt_rt
  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # https://bugs.llvm.org/show_bug.cgi?id=42455
#  install -Dm644 -t "$pkgdir/usr/share/man/man1" ../docs/llvm-bolt.1

  # Remove documentation sources
#  rm -r "$pkgdir"/usr/share/doc/llvm/html/{_sources,.buildinfo}
    rm -rf "$pkgdir"/tmp
}

# vim:set ts=2 sw=2 et:
