# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=llvm-bolt
pkgver=16.0.0rc3
_pkgver=16.0.0-rc3
pkgrel=1
pkgdesc="Post Link Optimizer from LLVM"
arch=('x86_64')
url="https://llvm-bolt.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('llvm-libs')
makedepends=('llvm' 'cmake' 'ninja' 'python-sphinx')
options=('!lto') # LTO causes a couple of test failures
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$_pkgver
source=($_source_base/bolt-$pkgver.src.tar.xz{,.sig}
        $_source_base/llvm-$pkgver.src.tar.xz{,.sig}
        $_source_base/cmake-$pkgver.src.tar.xz{,.sig}
        $_source_base/third-party-$pkgver.src.tar.xz{,.sig})
sha256sums=('dba5c70c3fe88b3a38b9180df82fbc9d1dfd55d68f41fddd6a90f9e17f8e5df9'
            'SKIP'
            '4ad8b2cc8003c86d0078d15d987d84e3a739f24aae9033865c027abae93ee7a4'
            'SKIP'
            '406d199ae3a16add84017f40458a5e7c31f9412937fcb518715af0a0eeafbc0c'
            'SKIP'
            '8986f29b634fdaa9862eedda78513969fe9788301c9f2d938f4c10a3e7a3e7ea'
            'SKIP')

prepare() {
  # https://bugs.llvm.org/show_bug.cgi?id=49228
  mv cmake{-$pkgver.src,}
  mv third-party{-$pkgver.src,}
  cd bolt-$pkgver.src
  mkdir build
}

build() {
  cd bolt-$pkgver.src/build

  local cmake_args=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_DOCDIR=share/doc
    -DCMAKE_SKIP_RPATH=ON
    -DLLVM_BUILD_DOCS=ON
    -DLLVM_ENABLE_SPHINX=ON
    -DLLVM_EXTERNAL_LIT=/usr/bin/lit
    -DLLVM_INCLUDE_TESTS=ON
    -DLLVM_LINK_LLVM_DYLIB=ON
    -DLLVM_MAIN_SRC_DIR="$srcdir/llvm-$pkgver.src"
    -DSPHINX_WARNINGS_AS_ERRORS=OFF
  )
  cmake .. "${cmake_args[@]}"
  ninja
}

check() {
  cd bolt-$pkgver.src/build
  ninja check-bolt
}

package() {
  cd bolt-$pkgver.src/build

  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # https://bugs.llvm.org/show_bug.cgi?id=42455
  install -Dm644 -t "$pkgdir/usr/share/man/man1" ../docs/llvm-bolt.1

  # Remove documentation sources
  rm -r "$pkgdir"/usr/share/doc/llvm-bolt/html/{_sources,.buildinfo}
}

# vim:set ts=2 sw=2 et:
