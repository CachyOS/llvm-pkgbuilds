# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing () gmail () com>

pkgname=clazy
pkgver=1.11
pkgrel=10
pkgdesc='Qt oriented code checker based on clang framework'
url='https://www.kdab.com/'
license=(GPL-2.0-or-later)
depends=(clang
         gcc-libs
         glibc
         llvm-libs
         sh)
makedepends=(cmake
             llvm)
arch=(x86_64)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
	clazy-clang-15-support.patch
	clazy-clang-16-support.patch
	clazy-clang-17-support.patch
	clazy-clang-18-support.patch)
sha256sums=('66165df33be8785218720c8947aa9099bae6d06c90b1501953d9f95fdfa0120a'
            'SKIP'
            '2f25e5e22e08abe2539429cbf398c1c5abbe4828daae9386fff1bb6dbebaac75'
            '67cbbee74c2e4d1ef7f50e0de3d62582386f29171a6f6b27dc41afff1787a540'
            'adee5b1785423564846aaaa222d81686b55605d4a987f1d1c5816686a480e912')
validpgpkeys=(949014B23D24354DFD548E5457416A0ADCEF0EFE) # Sergio Martins <sergio.martins@kdab.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < clazy-clang-15-support.patch # Fix build with LLVM 15
  patch -d $pkgname-$pkgver -p1 < clazy-clang-16-support.patch # Fix build with LLVM 16
  patch -d $pkgname-$pkgver -p1 < clazy-clang-17-support.patch # Fix build with LLVM 17
  patch -d $pkgname-$pkgver -p1 < clazy-clang-18-support.patch # Fix build with LLVM 18
}

build() {
  cmake -B build $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
