# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=openmp
pkgver=15.0.6
pkgrel=1
pkgdesc="LLVM OpenMP Runtime Library"
arch=('x86_64')
url="https://openmp.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('llvm-libs' 'libelf' 'libffi')
makedepends=('llvm' 'clang' 'cmake' 'ninja')
options=('!lto') # https://bugzilla.redhat.com/show_bug.cgi?id=1988155
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($_source_base/openmp-$pkgver.src.tar.xz
		$_source_base/cmake-$pkgver.src.tar.xz)
sha256sums=('1ac6f159d81625d852a42676ff6d8820cb744c18d825a56d56a6d7aa389f5ac9'
            '7613aeeaba9b8b12b35224044bc349b5fa45525919625057fa54dc882dcb4c86')

prepare() {
  cd openmp-$pkgver.src
  mkdir build
  cp $srcdir/cmake-$pkgver.src/Modules/* $srcdir/openmp-$pkgver.src/cmake

}

build() {
  cd openmp-$pkgver.src/build

  local cmake_args=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBOMP_INSTALL_ALIASES=OFF
  )
  cmake .. "${cmake_args[@]}"
  ninja
}

package() {
  cd openmp-$pkgver.src/build

  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm "$pkgdir/usr/lib/libarcher_static.a"
}

# vim:set ts=2 sw=2 et:
